dnl ###
dnl ### NSC 2.2 -- BIND Config File Builder
dnl ### (c) 1997--1999 Martin Mares <mj@ucw.cz>
dnl ###

# Definition of primary domain

define(`PRIMARY', `divert(0)zone "$1" in {
	type master;
	file "ZONEDIR/$1";
};

divert(-1)')

# Definition of secondary domain

define(`SECONDARY', `divert(0)zone "$1" in {
	type slave;
	file "BAKDIR/$1";
	masters { $2; };
};

divert(-1)')

# Definition of reverse domain

define(`REVERSE', `divert(0)zone "revaddr($1).in-addr.arpa" in {
	type master;
	file "ZONEDIR/revaddr($1)";
};

divert(-1)')

# Definition of reverse sub-domain

define(`PARTIAL', `SECONDARY(revaddr($1).in-addr.arpa,$3)')
define(`PREVERSE', `REVERSE($1)')

# Definition of forwarders for both normal and slave mode

define(`FORWard', `patsubst(`$@',`,',`; ')')
define(`FORWArd', `divert(0)	forward $1;
	forwarders { FORWard(shift($@),)};
divert(-1)')

define(`FORWARD', `FORWArd(first, $@)')
define(`SLAVE', `FORWArd(only, $@)')

divert(-1)')

# Manual insertion of config file material

define(`CONFIG', `divert(0)$1
divert(-1)')

# BIND options

define(`OPTIONS', `divert(0)options {
	directory "ROOT";
	interface-interval 0;
$1};

zone "." in {
	type hint;
	file "root.cache";
};

divert(-1)')

divert(0)dnl
`#'
`#'	Name server configuration file
`#'	Generated by mkconf.m4 on curdate
`#'	Please don't edit manually
`#'

divert(-1)
