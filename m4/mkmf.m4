dnl ###
dnl ### NSC 2.2 -- Makefile Builder
dnl ### (c) 1997--1999 Martin Mares <mj@ucw.cz>
dnl ###

# Things we allow to override

define(`named_restart_cmd', `ndc reload')

# List of all version files available

define(`ALLVERS', CONF CACHE)
define(`ADDVER', `define(`ALLVERS', ALLVERS` $1')')

# Definition of primary domain

define(`PRIMARY', `divert(0)VERSDIR/$1 ZONEDIR/$1: CFDIR/$1
	`$'(NSC) CFDIR/$1 >ZONEDIR/$1 -DVERS=VERSDIR/$1

divert(-1)
ADDVER(VERSDIR/$1)
')

# Definition of reverse domain

define(`REVCF', ` patsubst(CFDIR/$1,`^cf/\(.*\)\+\(.*\)$',`ZONEDIR/\1+\2')')
define(`REVERSi', `define(`ev',revaddr($1))define(`ew',`shift(shift($@))')
divert(0)VERSDIR/ev ZONEDIR/ev:iterate(`REVCF', `ew')
	`$'(NSC)iterate(`REVCF', `ew') >ZONEDIR/ev -DVERS=VERSDIR/ev -DREVERSE=$2 -DREVBASE=$1

divert(-1)
ADDVER(VERSDIR/ev)
')

define(`REVERSE', `REVERSi($1,$@)')
define(`PREVERSE', `REVERSi($1,patsubst($1,`^\(.*\)\..*$',`\1'),shift($@))')

# Definition of partial reverse zone delegation (also called classless in-addr.arpa)

define(`PARTIAL', `divert(0)ZONEDIR/$1+$2:
	`$'(PGEN) >ZONEDIR/$1+$2 -DBASE=revaddr($1) -DFROM=patsubst(`$1', `^.*\.\(.*\)$', `\1') -DCOUNT=$2 -DSERV=shift(shift($@))

divert(-1)')

# Insertion of raw makefile material

define(`MAKEFILE', `divert(0)$1
divert(-1)')

# Last words

define(`cleanup', `divert(0)VERSDIR/.version: ALLVERS
	named_restart_cmd
	touch VERSDIR/.version

clean:
	find BAKDIR ZONEDIR -type f -maxdepth 1 | xargs rm -f

clobber: clean
	rm -f Makefile CONF

distclean: clobber
	find VERSDIR -type f -maxdepth 1 | xargs rm -f
')

divert(0)dnl
`#'
`#'	Nameserver Configuration Makefile
`#'	Generated by mkmf.m4 on curdate
`#'	Please don't edit manually
`#'

NSC=m4 NSCDIR/dnslib.m4 NSCDIR/nsc.m4
PGEN=m4 NSCDIR/dnslib.m4 NSCDIR/pgen.m4

all: VERSDIR/.version
m4wrap(`cleanup')
divert(-1)
