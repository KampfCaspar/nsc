dnl ###
dnl ### NSC -- Makefile Builder
dnl ### (c) 1997--2003 Martin Mares <mj@ucw.cz>
dnl ###
include(m4/dnslib.m4)

# Definition of primary domains; secondaries we needn't take care of

define(`PRIMARIES', `')

define(`nsc_prepend_cf_one', ` 'CFDIR/`$1')
define(`nsc_prepend_cf_multi', `nsc_iterate(`nsc_prepend_cf_one', $@)')
define(`PRIMARY', `divert(0)ZONEDIR/$1:nsc_prepend_cf_multi($@) $(DDEPS)
	`$'(NSC)nsc_prepend_cf_multi($@) >ZONEDIR/$1 -DVERS=VERSDIR/$1

divert(-1)
define(`PRIMARIES', PRIMARIES ZONEDIR/$1)
')

define(`REVERSE', `PRIMARY(nsc_if_v6($1,`nsc_revblock6($1)',`nsc_revaddr($1)'), shift($@))')

# Insertion of raw makefile material

define(`MAKEFILE', `divert(0)$1
divert(-1)')

# Last words

define(`nsc_cleanup', `divert(0)VERSDIR/.version: CFDIR/domains ROOTCACHE`'PRIMARIES
	NAMED_RESTART_CMD
	touch VERSDIR/.version

clean:
	find BAKDIR ZONEDIR -type f -maxdepth 1 | xargs rm -f

clobber: clean
	rm -f Makefile named.conf

distclean: clobber
	find VERSDIR -type f -maxdepth 1 | xargs rm -f
')

divert(0)dnl
`#'
`#'	Nameserver Configuration Makefile
`#'	Generated by NSCVER (mkmf.m4) on CURRENT_DATE
`#'	Please don't edit manually
`#'

`M4'=M4
NSC=$(`M4') m4/nsc.m4
DDEPS=m4/nsc.m4 m4/dnslib.m4 cf/config

all: VERSDIR/.version
m4wrap(`nsc_cleanup')
divert(-1)
